lexical HashClass = (lexical table_size) {
	"New hash(" + table_size.to_string_primitive + ")" -> $stdout

	lexical table = []
	lexical next = nil

	lexical get = (index) -> return {
		local hash = index.hash() % table_size
		if (hash < table.length()) {
			local item = table.at(hash)

			if (item) {
				if (item.at(0) == index) { return <- item.at(1) }
				if (next) { return <- next.get(index) }
			}
		}
		return <- undef
	}
	lexical set = (index, val) -> return {
		local hash = index.hash() % table_size
		"setting " + index + "(" + hash.to_string_primitive + ")" -> $stdout
		if (hash < table.length()) {
			local item = table.at(hash)
			if (item) {
				if (item.at(0) != index) {
					if (next == nil) {
						next = HashClass(table_size * 2)
					}
					return <- next.set(index, val)
				}
			}
		}
		table = table.replace(hash, [index,val])
		return <- val
	}

	(@msg) -> return {
		local id = bytecode(msg) {
			message_split_id
			pop
		}
		switch (id)
		case (@'get') {
			return <- msg -> get
		} case (@'set') {
			return <- msg -> set
		}
	}
}

local hash = HashClass(19)

local n = 0
while (n != 200) {
	"adding " + n.to_string_primitive -> $stdout
	hash.set("hi " + n.to_string_primitive, n+1)
	n += 1
}
while (n != 0) {
	hash.get("hi " + n.to_string_primitive) -> $stdout
	n -= 1
}
